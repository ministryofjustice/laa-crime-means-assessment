import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "3.1.2"
    id "org.sonarqube" version "4.0.0.2929"
    id "info.solidsoft.pitest" version "1.9.11"
    id "org.jsonschema2dataclass" version "6.0.0"
}

def versions = [
        mapstructVersion            : '1.3.1.Final',
        pitest                      : "1.4.10",
        springdocVersion            : "2.1.0",
        okhttpVersion               : "4.9.3",
        mockwebserverVersion        : "4.9.3",
        liquibase                   : "4.20.0",
        crimeCommonsVersion         : "2.2.0",
        mockitoInlineVersion        : "5.2.0",
        cucumberVersion             : "7.14.0",
        springCloudStubRunnerVersion: "4.0.4"
]


group = "uk.gov.justice.laa.crime"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


repositories {
    mavenCentral()
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)

    implementation platform("io.sentry:sentry-bom:6.17.0")
    implementation("io.sentry:sentry-spring-boot-starter")
    implementation("io.sentry:sentry-logback")

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"

    implementation "org.postgresql:postgresql"
    implementation "org.apache.commons:commons-lang3"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "org.liquibase:liquibase-core:$versions.liquibase"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springdocVersion"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-spring-boot-starter-rest-client:$versions.crimeCommonsVersion"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation "org.mapstruct:mapstruct:$versions.mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$versions.mapstructVersion"

    testImplementation "com.h2database:h2"
    testImplementation "org.mockito:mockito-inline:$versions.mockitoInlineVersion"
    testImplementation "org.pitest:pitest:$versions.pitest"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "com.squareup.okhttp3:okhttp:$versions.okhttpVersion"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner:$versions.springCloudStubRunnerVersion"

    //  cucumber
    testImplementation platform("io.cucumber:cucumber-bom:$versions.cucumberVersion")
    testImplementation "io.cucumber:cucumber-java:$versions.cucumberVersion"
    testImplementation "io.cucumber:cucumber-spring:$versions.cucumberVersion"
    testImplementation "io.cucumber:cucumber-junit-platform-engine:$versions.cucumberVersion"
    testImplementation 'org.junit.platform:junit-platform-suite:1.10.0'
}

configurations {
    all {
        exclude group: 'junit', module: 'junit'
    }
}

test {
    useJUnitPlatform()
    dependsOn "cleanTest"
    finalizedBy jacocoTestReport

}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            "**/model/common/stateless/**",
                            "**/entity/**",
                            "**/dto/**",
                            "**/config/**"
                    ])
        }))
    }

}

pitest {
    targetClasses = ["uk.gov.justice.laa.crime.meansassessment.service.*", "uk.gov.justice.laa.crime.meansassessment.controller.*", "uk.gov.justice.laa.crime.meansassessment.validation.*"]
    excludedClasses = ["uk.gov.justice.laa.crime.meansassessment.dto.*", "uk.gov.justice.laa.crime.meansassessment.mapper.*", "uk.gov.justice.laa.crime.meansassessment.CrimeMeansAssessmentApplication"]
    threads = 5
    outputFormats = ["XML", "HTML"]
    timestampedReports = false
    mutationThreshold = 60
}

tasks["sonar"].dependsOn test

sonar {
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/model/common/stateless/**, **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java, **/**StoredProcedureRepository.java"
        property "sonar.coverage.exclusions", "**/CrimeMeansAssessmentApplication.java, , **/StatelessCucumberTest.java"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.projectKey", "ministryofjustice_laa-crime-means-assessment"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crime-means-assessment"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"

    }
}

jsonSchema2Pojo {
    executions {
        main {
            methods.builders = true
            methods.buildersDynamic = true
            methods.annotateJsr303Jakarta = true
            fields.floatUseBigDecimal = true
            dateTime.dateTimeType = "java.time.LocalDateTime"
            klass.targetPackage = "uk.gov.justice.laa.crime.meansassessment.model.common"
            io.source.setFrom files("${project.rootDir}/src/main/resources/schemas")
        }
    }
}